buildscript {
    ext {
        kotlin_version = '1.2.60'
        osgi_platform = 'win32.win32.x86'
        swt_version = '3.106.3'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
    }
}

group 'com.github.imeszaros'
version '1.0'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'edu.sc.seis.launch4j'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.eclipse.platform:org.eclipse.swt.win32.win32.x86:$swt_version"
    compile 'com.uchuhimo:konf:0.10'
    compile 'org.jsoup:jsoup:1.11.3'
    compile 'com.github.imeszaros:buxfer-api:v0.1'
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.$osgi_platform:$swt_version")
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.imeszaros.vb2buxfer.MainKt'
    }
}

launch4j {
    productName = 'Volksbank -> Buxfer'
    outfile = 'vb2buxfer.exe'
    mainClassName = 'com.github.imeszaros.vb2buxfer.MainKt'
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    icon = "${projectDir}/icon.ico"
    jreRuntimeBits = 32
    headerType = 'jniGui32'
}